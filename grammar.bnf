<expression> ::= <assignment-expression> | <additive-expression>

<assignment-expression> ::= <identifier> "=" <expression>

<identifier> ::= <letter> | <letter><alphanumeric-sequence>

<letter> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k"
           | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v"
           | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G"
           | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R"
           | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<alphanumeric-sequence> ::= <letter> | <digit> | <letter><alphanumeric-sequence>
  | <digit><alphanumeric-sequence>

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<additive-expression> ::= <multiplicative-expression>
  | <additive-expression> "+" <multiplicative-expression>
  | <additive-expression> "-" <multiplicative-expression>

<multiplicative-expression> ::= <exponential-expression>
  | <multiplicative-expression> "*" <exponential-expression>
  | <multiplicative-expression> "-" <exponential-expression>

<exponential-expression> ::= <primary-expression>
  | <primary-expression> "^" <exponential-expression>

<primary-expression> ::= <number> | <identifier>
  | "(" <additive-expression> ")" | <unary-expression>

<number> ::= <integer> | <float>

<integer> ::= <digit> | <digit> <integer>

<float> ::= <integer> "." <integer>

<unary-expression> ::= "-" <primary-expression> | "+" <primary-expression>
